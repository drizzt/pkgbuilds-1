# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-mojo-plugged-$(echo $USER | sha512sum | cut -c -128 | sha512sum | cut -c -8)-git
_gitname=mojo.vim
pkgver=20140206
pkgrel=1
pkgdesc="Vim syntax for mojo epl templates in Mojolicious projects ($USER)"
arch=('any')
depends=('vim')
makedepends=('git')
checkdepends=('make')
optdepends=('perl-text-vimcolor: run tests')
groups=('vim-plugins')
url="https://github.com/yko/mojo.vim"
license=('PerlArtistic')
source=(git+https://github.com/yko/$_gitname)
sha256sums=('SKIP')
install=mojo.install

pkgver() {
  cd $_gitname
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd $_gitname
  git config remote.origin.url https://git::@${source##git+https://}.git

  msg 'Fixing Makefile...'
  sed -i 's@prove@/usr/bin/core_perl/prove@' Makefile
}

check() {
  cd $_gitname

  msg 'Testing...'
  make test || return 1
}

package() {
  cd $_gitname
  install -dm 755 "$pkgdir/$HOME/.vim/plugged/$_gitname"
  for _file in `ls -A`; do
    cp -dpr --no-preserve=ownership $_file "$pkgdir/$HOME/.vim/plugged/$_gitname"
  done
  find "$pkgdir" -mindepth 1 -maxdepth 1 -exec chown -R $USER:users '{}' \;
}
