# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-maven-compiler-plugged-$(echo $USER | sha512sum | cut -c -128 | sha512sum | cut -c -8)-git
_gitname=maven-compiler.vim
pkgver=20141101
pkgrel=1
pkgdesc="Maven compiler module / library for Vim ($USER)"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/JalaiAmitahl/maven-compiler.vim"
license=('custom:vim')
source=(git+https://github.com/JalaiAmitahl/maven-compiler.vim)
sha256sums=('SKIP')
install=maven-compiler.install

pkgver() {
  cd $_gitname
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd $_gitname
  git config remote.origin.url https://git::@${source##git+https://}.git
}

package() {
  cd $_gitname
  install -dm 755 "$pkgdir/$HOME/.vim/plugged/$_gitname"
  find . -mindepth 1 -maxdepth 1 -exec \
    cp -dpr --no-preserve=ownership '{}' "$pkgdir/$HOME/.vim/plugged/$_gitname" \;
  find "$pkgdir" -mindepth 1 -maxdepth 1 -exec chown -R $USER:users '{}' \;
}
