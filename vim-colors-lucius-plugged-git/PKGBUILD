# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-colors-lucius-plugged-$(echo $USER | sha512sum | cut -c -128 | sha512sum | cut -c -8)-git
_gitname=vim-lucius
pkgver=20140707
pkgrel=1
pkgdesc="Light and dark color scheme for GUI and 256 color terminal Vim, with several options to customize to your own tastes ($USER)"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/jonathanfilip/vim-lucius"
license=('custom:vim')
source=(git+https://github.com/jonathanfilip/vim-lucius)
sha256sums=('SKIP')
install=lucius.install

pkgver() {
  cd $_gitname
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd $_gitname
  git config remote.origin.url https://git::@${source##git+https://}.git
}

package() {
  cd $_gitname
  install -dm 755 "$pkgdir/$HOME/.vim/plugged/$_gitname"
  find . -mindepth 1 -maxdepth 1 -exec \
    cp -dpr --no-preserve=ownership '{}' "$pkgdir/$HOME/.vim/plugged/$_gitname" \;
  find "$pkgdir" -mindepth 1 -maxdepth 1 -exec chown -R $USER:users '{}' \;
}
