# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-misc-xolox-plugged-$(echo $USER | sha512sum | cut -c -128 | sha512sum | cut -c -8)-git
_gitname=vim-misc
pkgver=20140917
pkgrel=1
pkgdesc="Miscellaneous auto-load Vim scripts by Peter Odding ($USER)"
arch=('any')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
url="https://github.com/xolox/vim-misc"
license=('MIT')
source=(git+https://github.com/xolox/vim-misc)
sha256sums=('SKIP')
install=misc-xolox.install

pkgver() {
  cd $_gitname
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd $_gitname
  git config remote.origin.url https://git::@${source##git+https://}.git
}

package() {
  cd $_gitname
  install -dm 755 "$pkgdir/$HOME/.vim/plugged/$_gitname"
  find . -mindepth 1 -maxdepth 1 -exec \
    cp -dpr --no-preserve=ownership '{}' "$pkgdir/$HOME/.vim/plugged/$_gitname" \;
  find "$pkgdir" -mindepth 1 -maxdepth 1 -exec chown -R $USER:users '{}' \;
}
